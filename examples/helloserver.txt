### Set up interrupt vectors
loadwi 15 0

# first vector
strapr 15 .intr_vec

# Point to next vector
addi 15 1 15

# Remaining vectors
.write_vector
strapr 15 .exit
addi 15 1 15
jmpgei 15 10 .write_done  # 16 vectors
jmp .write_vector  # Next vector

.write_done

eni  # Reenable interrupts

#### Program

### Set up interrupt controller
savewi C0 FFFFFED2
savewi .async_intr FFFFFED6
savebi 1 FFFFFEDA
savewi 0 FFFFFECE

### Get socket
# IPv4
savewi 1 FFFFE95F

# TCP
savewi 1 FFFFF863

# Open socket
savewi 1 FFFFF86B

# Did we succeed?
loadw A FFFFF873
jmplti A 0 .exit

# Load socket into B
loadw B FFFFF867

### Bind
# 0.0.0.0
savewi 0 FFFFF84F
savewi 0 FFFFF853
savewi 0 FFFFF857
savewi 0 FFFFF85B

# Port 6969
savewi 1B39 FFFFF86F

# Perform bind
savewi 2 FFFFF86B

# Did we succeed?
loadw A FFFFF873
jmplti A 0 .exit

### Listen!
savewi 4 FFFFF86B

# Did we succeed?
loadw A FFFFF873
jmplti A 0 .exit

### Set up interrupt event for listening
savewi 1 FFFFF86F
savewi F FFFFF86B

### Main loop
.loop

# Wait for an interrupt
wait

# And back around
jmp .loop

### Accept routine
.async_intr

# Load the connection from the async handle reg
loadw A FFFFF87B

# Store it in the main controller register
savew A FFFFF867

# Accept the connection
savewi 5 FFFFF86B

# Did it give us an error?
loadw A FFFFF873
jmpgti A 0 .async_intr_end

# Copy "Hello world!\r\n" to the buffer
savewi 68656C6C FFFFF9AF
savewi 6F20776F FFFFF9B3
savewi 726C6421 FFFFF9B7
savewi 0D0A0000 FFFFF9BB

# Save buffer length
savewi E FFFFF8AB

# Erase params before send
savewi 0 FFFFF86F

# Perform send
savewi A FFFFF86B

# Close the client connection
savewi 6 FFFFF86B

# Restore the listening socket
savew B FFFFF867

.async_intr_end
# Reenable interrupts
savewi 0 FFFFFECE 0

# Set handler as done
savewi 11 FFFFF86B
ret

### End program
.exit
halt

### Interrupt vector
.intr_vec
jmp FFFFFEEA
